"pkiadm" (* slogan = "Perform CRUD operations on profiles, entities and certificates" *) WS "rem" (* hide = "true" *) WS ( "CA" (* slogan = "Manage the Certificate(s) of a CA Entity" *) | "EE" (* slogan = "Manage the Certificate(s) of an EE Entity" *) ) REVOKE_OPTIONS

REVOKE_OPTIONS ::= REVMGMT_REVOKE REVOKE_IDENTIFICATION
REVOKE_IDENTIFICATION ::= ( REV_ENTITY_NAME [ REVOCATION_REASON  [ INVALIDITY_DATE ] ]
                          | CERTIFICATE_IDENTIFIER  REVOCATION_REASON INVALIDITY_DATE
                          | DN_BASED_IDENTIFIER REVOCATION_REASON INVALIDITY_DATE )
CERTIFICATE_IDENTIFIER ::= ISSUER_NAME SERIAL_NO
DN_BASED_IDENTIFIER ::= SUBJECT_DN ISSUER_DN SERIAL_NO
REVOCATION_REASON ::= ( REASON_CODE | REASON_TEXT )

REVMGMT_REVOKE ::= WS ( "--revoke" (* slogan = "Revoke the certificate(s) of an entity. Tip: -rev for short" *) | "-rev" (* hide = "true" *) )
ISSUER_NAME ::= WS ( "--issuername" (* slogan = "Revoke the certificate(s) of an entity by issuer name. Tip: -isrn for short" *) | "-isrn" (* hide = "true" *) ) WS <issuer_name> (* slogan = "Specify the Issuer name of the entity certificate to revoke. E.g. 'ENMInfrastructureCA'" *)
REV_ENTITY_NAME ::= WS ( "--entityname" (* slogan = "Revoke the certificate(s) of an entity by entity name. Tip: -en for short" *) | "-en" (* hide = "true" *) ) WS <entity_name> (* slogan = "Specify the Entity name of the certificate to revoke. E.g. 'ERBS'" *)
SERIAL_NO ::= WS ( "--serialno" (* slogan = "Revoke the certificate(s) of an entity by serial number. Tip: -sno for short" *) | "-sno" (* hide = "true" *) ) WS <certificate_serial_number> (* slogan = "Specify the serial number of the entity certificate to revoke. E.g. 5f5b76b3d4792483" *) { "," (* slogan = "Comma used to add Certificate Serial Number to a list" *) <certificate_serial_number> (* slogan = "Specify the Serial number of the entity certificate to revoke. E.g. 5f5b76b3d4792483" *) }
ISSUER_DN ::= WS ( "--issuerDN" (* slogan = "Revoke the certificate(s) of an entity by issuer DN. Tip: -isrDN for short" *) | "-isrDN" (* hide = "true" *) ) WS <issuer_DN> (* regex = "^(?:\-\d|[\w\$\=\"]+)[\w\$\-]*$" slogan = "Specify the Issuer DN of the entity certificate to revoke. E.g. CN=ENMSUBCA,OU=ERICSSON" *) { "," (* slogan = "Comma used to add another Issuer DN to a list" *) <issuer_DN> (* regex = "^(?:\-\d|[\w\$\=\"]+)[\w\$\-]*$" slogan = "Specify the Issuer DN of the entity certificate to revoke. E.g. CN=ENMSUBCA,OU=ERICSSON" *) }
SUBJECT_DN ::= WS ( "--subjectDN" (* slogan = "Revoke the certificate(s) of an entity by subject DN. Tip: -subDN for short" *) | "-subDN" (* hide = "true" *) ) WS <subject_DN> (* regex = "^(?:\-\d|[\w\$\=\"]+)[\w\$\-]*$" slogan = "Specify the Subject DN of the entity certificate to revoke. E.g. CN=ERBS,OU=ERICSSON" *) { "," (* slogan = "Comma used to add another Subject DN to a list" *) <subject_DN> (* regex = "^(?:\-\d|[\w\$\=]+)[\w\$\-\"]*$" slogan = "Specify the Subject DN of the entity certificate to revoke. E.g. CN=ERBS,OU=ERICSSON" *) }
REASON_CODE ::= WS ( "--reasoncode" (* slogan = "Revoke certificate(s) of an entity by specific reason code. Tip: -rc for short" *) | "-rc" (* hide = "true" *) ) REASON_CODE_VALUE
REASON_CODE_VALUE ::= WS ( "0" (* slogan = "Reason code 0" *) | "1" (* slogan = "Reason code 1" *) | "2" (* slogan = "Reason code 2" *) | "3" (* slogan = "Reason code 3" *) | "4" (* slogan = "Reason code 4" *) | "5" (* slogan = "Reason code 5" *) | "6" (* slogan = "Reason code 6" *) | "7" (* slogan = "Reason code 7" *) | "8" (* slogan = "Reason code 8" *) | "9" (* slogan = "Reason code 9" *) | "10" (* slogan = "Reason code 10" *) )
REASON_TEXT ::= WS ( "--reasontext" (* slogan = "Revoke certificate(s) of an entity by specific reason text. Tip: -rt for short" *) | "-rt" (* hide = "true" *) ) REASON_TEXT_VALUE
REASON_TEXT_VALUE ::= WS ( "unspecified" | "keyCompromise" | "cACompromise" | "affiliationChanged" | "superseded" | "cessationOfOperation" | "certificateHold" | "removeFromCRL" | "privilegeWithdrawn" | "aACompromise" )
INVALIDITY_DATE ::= WS ( "--invaliditydate" (* slogan = "Revoke the certificate(s) of an entity by invalidity date. Tip: -ind for short" *) | "-ind" (* hide = "true" *) ) WS <certificate_invalidity_date> (* inputType = "wrapDoubleQuotes" slogan = "In quotation marks, specify the Invalidity date of entity certificate to revoke. E.g. "2014-11-21 23:11:10"" *)
